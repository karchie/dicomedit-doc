% \documentclass[handout]{beamer}
\documentclass{beamer}  % include pauses for talk slides
\usetheme{Singapore}
\usepackage{color,enumerate,fancyvrb,hyperref,verbatim}
\title[DicomEdit]{DicomEdit\\A language for DICOM metadata modification}
\author{Kevin A. Archie}
\institute{Washington University Neuroinformatics Research Group}
\date{19 December 2014}

\mathchardef\mhyphen="2D

\begin{document}

\AtBeginSubsection[]
{
  \begin{frame}<beamer>
    \tableofcontents[currentsection,currentsubsection]
  \end{frame}
}

\begin{frame}
\titlepage
\end{frame}

\section{Introduction}

\begin{frame}{What is DicomEdit?}
\begin{itemize}
\pause \item Small language for specifying changes to DICOM metadata
\pause \item a.k.a. ``anon script'' language
\pause \item Interpreter is a modular component:
\url{https://bitbucket.org/dicomedit/dicomedit}
\pause \item Similar in spirit (but not syntax) to MIRC Anonymizer language
\end{itemize}
\end{frame}

\subsection{History}
\begin{frame}{History}
\begin{itemize}
\pause \item DicomBrowser (late 2006; v1.0 released 1 March 2007)
\pause \item Scriptable modifications (in DicomBrowser v1.0)
\pause \item DicomSummarize/DicomRemap (first command-line DicomEdit, early 2008)
\pause \item XNAT upload applet (2008)
\pause \item XNAT archiving (2011)
\end{itemize}
\end{frame}

\section{The language}
\subsection{Operations}
\begin{frame}[fragile]{Operations}

\begin{verbatim}
// Assign a uniform Institution Name
(0008,0080) := "WUSM"
\end{verbatim}

\pause
\begin{verbatim}
// Delete the Institution Address
// and Frame of Reference UID
- (0008,0081)
- (0020,0052)
\end{verbatim}

\end{frame}

\begin{frame}[fragile]{Conditional operations}

\begin{verbatim}
// Use Referring Physician's Name
// to identify research group;
// put research group identifier
// into Clinical Trial Protocol ID
(0008,0090) = "Lee^Ann"   : (0012,0020) := "P-035"
(0008,0090) = "Hill^John" : (0012,0020) := "P-028"
\end{verbatim}

\pause
\begin{verbatim}
// Series with 3-digit numbers are derived data
(0020,0011) ~ "\\d\\d\\d" : \
  (0008,103e) := format["SECONDARY {0}", (0008,103e)]
\end{verbatim}

\end{frame}

\subsection{User-defined variables}
\begin{frame}[fragile]{User-defined variables}

\begin{verbatim}
// Allow the user to edit Patient ID
// Initial value is the pre-modification value
patientID := (0010,0020)
describe patientID "Subject ID"
(0010,0020) := patientID
\end{verbatim}

\end{frame}

\subsection{Built-in functions}
\begin{frame}[fragile]{Built-in functions}
\scriptsize
\begin{verbatim}
// Get visit ID from web service
// using Patient ID and Study Date
describe visurl hidden
visurl := format["http://localhost:3000/services/visitID?id={0}&date={1}", \
       urlEncode[(0010,0020)], urlEncode[(0008,0020)]]
describe visit hidden
visit := getURL[visurl]


// Generate new Study Description based on Patient ID, Visit ID, modality
describe studyDesc "Study Description"
studyDesc := format["{0}_{1}_{2}", (0010,0020), visit, (0008,0060)]
(0008,1030) := studyDesc
\end{verbatim}
\end{frame}

\begin{frame}[fragile]{Built-in functions: text formatting}
\begin{center}
\small
\begin{tabular}{ l p{1in} p{2in} }
Name & Arguments & Description \\ \hline

format & {\em format-string}, $v_1, v_2, \dots$ &
Formats the given values using the same syntax as java.text.MessageFormat \\

\pause
lowercase & {\em string} &
Converts all uppercase characters to lowercase \\

\pause
uppercase & {\em string} &
Converts all lowercase characters to uppercase \\

\pause
substring & {\em string, start\nobreakdash-index,
end\nobreakdash-index} &
Returns a substring, similar to \verb|s.substring(start,end)| \\

\end{tabular}
\end{center}

\pause
\hrule
\scriptsize
\begin{verbatim}
(0008,103e) := "SECONDARY {0} - {1}" (0008,103e), (0020,0011)
\end{verbatim}
is shorthand (probably deprecated) for
\begin{verbatim}
(0008,103e) := format["SECONDARY {0} - {1}", (0008,103e), (0020,0011)]
\end{verbatim}

\end{frame}

\begin{frame}[fragile]{Built-in functions: pattern matching}
\begin{center}
\small
\begin{tabular}{ l p{1in} p{2in} }
Name & Arguments & Description \\ \hline

\textmd{}replace & {\em string, target, \mbox{replacement}} &
Replaces all occurrences of \mbox{\em target} in the
given string with \mbox{\em replacement} \\

\pause
match & {\em value, pattern, i} &
Matches {\em value} against {\em pattern} and returns
the content of the $i$th matching group \\

\end{tabular}
\end{center}

\pause
\hrule
\vspace{2mm}
\footnotesize
\verb|match(value,pattern,i)| is comparable to:
\verb|java.util.regex.Pattern.compile(pattern).matcher(value).group(i)|
\end{frame}

\begin{frame}[fragile]{Built-in functions: web services}
\begin{center}
\small
\begin{tabular}{ l p{1in} p{2in} }
Name & Arguments & Description \\ \hline

getURL & {\em url} &
Retrieves the content of the resource at the given URL \\

\pause
urlEncode & {\em string} &
Encodes the given value so that it can be embedded in a URL \\

\end{tabular}
\end{center}

\pause
\hrule
\scriptsize
\begin{verbatim}
describe visurl hidden
visurl := format["http://localhost:3000/services/visitID?id={0}&date={1}", \
       urlEncode[(0010,0020)], urlEncode[(0008,0020)]]
visit := getURL[visurl]
\end{verbatim}

\end{frame}

\subsection{UID Remapping}
\begin{frame}[fragile]{hashUID}
The preferred method for generating new UIDs is the built-in function
hashUID:

\begin{verbatim}
(0008,010D) ~ ".+" : (0008,010D) := hashUID[(0008,010D)]
\end{verbatim}
\end{frame}

\begin{frame}[fragile]{UID Generator}
There's an older method for replacing UIDs:
\begin{verbatim}
// Reassign UIDs
(0020,000D) := new UID	// Study Instance UID
(0020,000E) := new UID	// Series Instance UID
(0008,0018) := new UID	// SOP Instance UID
(0008,1155) := new UID	// Referenced SOP Instance UID
\end{verbatim}
\pause
The generator method uses mutable state to retain study/series associations;
this works fine for DicomBrowser or the upload applet but fails in XNAT.
UID generators are now deprecated but you may see them in old code.
\end{frame}

\subsection{Tag Patterns}
\begin{frame}[fragile]{Top-level patterns}
Sometimes we're interested in classes of tags rather than particular values.
For example, vendor-private tags have odd group numbers.
DicomEdit now supports tag patterns for such cases.

\begin{verbatim}
-(XXX#,XXXX)       // remove all vendor-private tags
-(50XX,XXXX)       // remove Curve Data
\end{verbatim}

\pause
Tag patterns (as opposed to explicit tags) can be used only for lvalues: 
tags being assigned to or deleted. They are not valid as rvalues.

\begin{Verbatim}[commandchars=\\\{\}]
\textcolor{red}{(0010,0010) := (0029,00XX)   // No! Bad dog!}
\end{Verbatim}
\end{frame}

\begin{frame}[fragile]{Example: vendor-private tag patterns}

\scriptsize
\begin{verbatim}
// Begin retain all private fields needed
// for functional analysis
(0029,0010) = "SIEMENS CSA HEADER" : (0029,0010) := (0029,0010)
(0029,0010) = "SIEMENS CSA HEADER" : (0029,1008) := (0029,1008)
(0029,0010) = "SIEMENS CSA HEADER" : (0029,1009) := (0029,1009)
(0029,0010) = "SIEMENS CSA HEADER" : (0029,1010) := (0029,1010)
(0029,0010) = "SIEMENS CSA HEADER" : (0029,1018) := (0029,1018)
(0029,0010) = "SIEMENS CSA HEADER" : (0029,1019) := (0029,1019)
(0029,0010) = "SIEMENS CSA HEADER" : (0029,1020) := (0029,1020)
// End retain private fields

-(XXX#,XXXX) // Private Attributes
\end{verbatim}
\end{frame}

\begin{frame}[fragile]{Tagpaths}
DicomEdit supports accessing tags in sequences:

\scriptsize
\begin{verbatim}
-(0008,1140)[1](0008,1155)  // delete field from second item in SQ
\end{verbatim}
\pause

\normalsize
Tagpath patterns are valid lvalues:
\scriptsize
\begin{verbatim}
-(0008,1140)[X](0008,XXXX)  // all the 0008 fields in all SQ items
\end{verbatim}

\pause
\hrule
\vspace{2mm}
\footnotesize
\verb|X| is the only valid sequence index pattern. Convince me that you
really need to access only the odd SQ items and I'll reconsider.
\end{frame}

\begin{frame}[fragile]{Nested tags}
Some tags might appear in the top-level object, or buried in a sequence.
There's now special syntax for this case:

\scriptsize
\begin{verbatim}
-*(0010,1010)  // remove Patient's Age wherever it's found
\end{verbatim}

\pause
\normalsize
Again, this is lvalue-only.
\scriptsize
\begin{Verbatim}[commandchars=\\\{\}]
*(0010,0010) := "Bucky"      // OK
\textcolor{red}{(0010,0010) := *(0010,0010)  // Ow! Quit it!}
\end{Verbatim}
\end{frame}

\subsection{Phases of execution}
\begin{frame}[fragile]{Phases of script execution}
\begin{enumerate}
\item Parsing (global)
  \begin{enumerate}[i]
    \pause
    \item Abstract Syntax Tree (AST) construction
    \pause
    \item Variable initialization
    \pause
    \item Variable value assignment
  \end{enumerate}
\pause
\item Application (object-by-object)
  \begin{enumerate}[i]
    \pause
    \item Value evaluation
    \pause
    \item Tag assignment
    \pause
    \item Object export
  \end{enumerate}
\end{enumerate}

\pause
Variable initialization from DICOM attributes is an ill-posed
problem. DicomBrowser joins values into a comma-separated list, while
the upload applet uses a single value from an arbitrary DICOM object.

\end{frame}

\subsection{Example: DIAN script for XNAT}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
// Common DIAN anonymization script v. 001, 2009-08-21
// by Kevin Archie, karchie@wustl.edu
(0008,0080) = "Columbia University" : (0008,0080) := "DIAN_010" // Institution Name
(0008,0080) = "Hatch Ctr 1.5T @ Columbia" : (0008,0080) := "DIAN_010" 
(0008,0080) = "CCIR 3T" : (0008,0080) := "DIAN_011" 
(0008,0080) = "Center for Clinical Imaging Research" : (0008,0080) := "DIAN_011" 
(0008,0080) = "Washington University" : (0008,0080) := "DIAN_011" 
(0008,0080) = "UCLA BRAIN MAPPING CENTER" : (0008,0080) := "DIAN_035" 
(0008,0080) = "Brain Mapping" : (0008,0080) := "DIAN_035" 
(0008,0080) = "Ser.Nr. 58013" : (0008,0080) := "DIAN_035"
(0008,0080) = "Indiana University Radiology Associates" : (0008,0080) := "DIAN_037" 
(0008,0080) = "INDIANA UNIVERSITY RESEARCH" : (0008,0080) := "DIAN_037" 
(0008,0080) = "Martinos Center Bay 4" : (0008,0080) := "DIAN_094" 
(0008,0080) = "BROWN UNIVERSITY MRF" : (0008,0080) := "DIAN_941" 
(0008,0080) = "POW Medical Research Institute" : (0008,0080) := "DIAN_950"
(0008,0080) = "Diagnostic MRI Services" : (0008,0080) := "DIAN_950"
(0008,0080) = "Austin Health, Centre for PET" : (0008,0080) := project 
(0008,0080) = "<Full Institution Name>" : (0008,0080) := "DIAN_951"
(0008,0080) = "Brain Research Institute" : (0008,0080) := "DIAN_951"
(0008,0080) = "SKG Radiology Subiaco" : (0008,0080) := "DIAN_952"
(0008,0080) = "0ECU 0952" : (0008,0080) := "DIAN_952"
(0008,0080) = "0ECU0952" : (0008,0080) := "DIAN_952"
(0008,0080) = "The WA PET/Cyclotron Service" : (0008,0080) := "DIAN_952"
(0008,0080) = "National Hospital for Neurology & Neurosurgery" : (0008,0080) := "DIAN_953"
(0008,0080) = "nhnn" : (0008,0080) := "DIAN_953"
(0008,0080) = "NHNN" : (0008,0080) := "DIAN_953"
(0008,0080) = "Addenbrookes Cambridge" : (0008,0080) := "DIAN_953"
(0008,0080) = "Hospital" : (0008,0080) := "DIAN_Test"
\end{verbatim}
\end{frame}

\begin{frame}[fragile]
\tiny
\begin{verbatim}
- (0008,0050)     // Accession Number
- (0010,0021)     // Issuer of Patient ID
- (0010,0030)     // Patient's Birth Date
- (0010,0032)     // Patient's Birth Time
- (0010,0050)     // Patient's Insurance Plan Code SQ
- (0010,0101)     // Patient's Primary Language Code SQ
- (0010,1000)     // Other Patient IDs
- (0010,1001)     // Other Patient Names
- (0010,1002)     // Other Patient IDs SQ
- (0010,1005)     // Patient's Birth Name
- (0010,1010)     // Patient's Age
- (0010,1040)     // Patient's Address
- (0010,1060)     // Patient's Mother's Birth Name
- (0010,1080)     // Military Rank
- (0010,1081)     // Branch of Service
- (0010,1090)     // Medical Record Locator
- (0010,2000)     // Medical Alerts
- (0010,2110)     // Allergies
- (0010,2150)     // Country of Residence
- (0010,2152)     // Region of Residence
- (0010,2154)     // Patient's Telephone Numbers
- (0010,2160)     // Ethnic Group
- (0010,2180)     // Occupation
- (0010,21A0)     // Smoking Status
- (0010,21B0)     // Additional Patient History
- (0010,21C0)     // Pregnancy Status
- (0010,21D0)     // Last Menstrual Date
- (0010,21F0)     // Patient's Religious Preference
- (0010,2203)     // Patient's Sex Neutered
- (0010,2297)     // Responsible Person
- (0010,2298)     // Responsible Person Role
- (0010,2299)     // Responsible Organization
- (0038,0500)     // Patient State
- (0038,0100)     // Pertinent Documents SQ
\end{verbatim}
\end{frame}

\begin{frame}[fragile]
\tiny
\begin{Verbatim}[commandchars=\\\{\}]
(0012,0063) := "DIAN common deidentification v001"

session := \textcolor{red}{makeSessionLabel}[format["\{0\}_v##_\{1\}", subject, lowercase[modalityLabel]]]

// Use a regular expression to pull the visit ID out of the session label
visit := match[session,".*_(v[0-9]+)_.*",1]

// Don't show the visit field in the assign session variables page
describe visit hidden                           

// Use visit to set the named XNAT field
\textcolor{red}{export visit "xnat:experimentData/visit_id"}

// Set XNAT identifying fields
(0008,1030) := project
(0010,0010) := subject
(0010,0020) := session
\end{Verbatim}
\end{frame}

\subsection{Example: DICOM standard deidentification}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
// Basic Application Level Confidentiality Profile
// Adapted from DICOM Standard, PS 3.15-2011, Table E.1-1
// Copyright (c) 2012 Washington University
// Author: Kevin A. Archie <karchie@wustl.edu>
// NOTE: requires DicomEdit v3.0.0 or later
(0012,0062) := "YES"                 // Patient Identity Removed
(0008,0050) ~ ".+" : (0008,0050) := ""  // Accession Number
-(0018,4000)                         // Acquisition Comments
-(0040,0555)                         // Acquisition Context Sequence
-(0008,0022)                         // Acquisition Date
-(0008,002A)                         // Acquisition DateTime
-(0018,1400)                         // Acquisition Device Processing Description
-(0018,9424)                         // Acquisition Protocol Description
-(0008,0032)                         // Acquisition Time
-(0040,4035)                         // Actual Human Performers Sequence
-(0010,21B0)                         // Additional Patient's History
-(0038,0010)                         // Admission ID
-(0038,0020)                         // Admitting Date
-(0008,1084)                         // Admitting Diagnoses Code Sequence
-(0008,1080)                         // Admitting Diagnoses Description
-(0038,0021)                         // Admitting Time
-(0000,1000)                         // Affected SOP Instance UID
-(0010,2110)                         // Allergies
-(4000,0010)                         // Arbitrary
-(0040,A078)                         // Author Observer Sequence
-(0010,1081)                         // Branch of Service
-(0018,1007)                         // Cassette ID
-(0040,0280)                         // Comments on Performed Procedure Step
\end{verbatim}
\end{frame}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
(0020,9161) ~ "\\d[\\d\\.]+" : (0020,9161) := hashUID[(0020,9161)]
-(0020,9161)                         // Concatenation UID
-(0040,3001)                         // Confidentiality Constraint on Patient Data Description
(0070,0084) := ""                    // Content Creator's Name
-(0070,0086)                         // Content Creator's Identification Code Sequence
(0008,0023) ~ ".+" : (0008,0023) := ""  // Content Date
-(0040,A730)                                      // Content Sequence
(0008,0033) ~ ".+" : (0008,0033) := ""    // Content Time
(0008,010D) ~ "\\d[\\d\\.]+" : (0008,010D) := hashUID[(0008,010D)]
-(0008,010D)                         // Context Group Extension Creator UID
(0018,0010) ~ ".+" : (0018,0010) := ""           // Contrast Bolus Agent
-(0018,A003)                         // Contribution Description
-(0010,2150)                         // Country of Residence
(0008,9123) ~ "\\d[\\d\\.]+" : (0008,9123) := hashUID[(0008,9123)]
-(0008,9123)                         // Creator Version UID
-(0038,0300)                         // Current Patient Location
-(50XX,XXXX)                         // Curve Data
-(0008,0025)                         // Curve Date
-(0008,0035)                         // Curve Time
-(0040,A07C)                         // Custodial Organization Sequence
-(FFFC,FFFC)                         // Data Set Trailing Padding
-(0008,2111)                         // Derivation Description
-(0018,700A)                         // Detector ID
-(0018,1000)                         // Device Serial Number
(0018,1002) ~ "\\d[\\d\\.]+" : (0018,1002) := hashUID[(0018,1002)]
-(0018,1002)                         // Device UID
-(0400,0100)                         // Digital Signature UID
-(FFFA,FFFA)                         // Digital Signatures Sequence
\end{verbatim}
\end{frame}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
(0020,9164) ~ "\\d[\\d\\.]+" : (0020,9164) := hashUID[(0020,9164)]
-(0020,9164)                         // Dimension Organization UID
-(0038,0040)                         // Discharge Diagnosis Description
-(4008,011A)                         // Distribution Address
-(4008,0119)                         // Distribution Name
(300A,0013) ~ "\\d[\\d\\.]+" : (300A,0013) := hashUID[(300A,0013)]
-(300A,0013)                         // Dose Reference UID
-(0010,2160)                         // Ethnic Group
(0008,0058) ~ "\\d[\\d\\.]+" : (0008,0058) := hashUID[(0008,0058)]
-(0008,0058)                         // Failed SOP Instance UID List
(0070,031A) ~ "\\d[\\d\\.]+" : (0070,031A) := hashUID[(0070,031A)]
-(0070,031A)                         // Fiducial UID
(0040,2017) ~ ".+" : (0040,2017) := ""  // Filler Order Number of Imaging Service Request
-(0020,9158)                         // Frame Comments
(0020,0052) ~ "\\d[\\d\\.]+" : (0020,0052) := hashUID[(0020,0052)]
-(0020,0052)                         // Frame of Reference UID
-(0018,1008)                         // Gantry ID
-(0018,1005)                         // Generator ID
-(0070,0001)                         // Graphic Annotation Sequence (NOTE: should be dummy value)
-(0040,4037)                         // Human Performers Name
-(0040,4036)                         // Human Performers Organization
-(0088,0200)                         // Icon Image Sequence
-(0008,4000)                         // Identifying Comments
-(0020,4000)                         // Image Comments
-(0028,4000)                         // Image Presentation Comments
-(0040,2400)                         // Image Service Request Comments
-(4008,0300)                         // Impressions
-(0008,0012)			     // Instance Creation Date (NOTE: not in deident standard)
-(0008,0013)			     // Instance Creation Time (NOTE: not in deident standard)
\end{verbatim}
\end{frame}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
(0008,0014) ~ "\\d[\\d\\.]+" : (0008,0014) := hashUID[(0008,0014)]
-(0008,0014)                         // Instance Creator UID
-(0008,0081)                         // Institution Address
-(0008,0082)                         // Institution Code Sequence
-(0008,0080)                         // Institution Name
-(0008,1040)                         // Institutional Department Name
-(0010,1050)                         // Insurance Plan Identification
-(0040,1011)                         // Intended Recipients of Results Identification Sequence
-(4008,0111)                         // Interpretation Approver Sequence
-(4008,010C)                         // Interpretation Author
-(4008,0115)                         // Interpretation Diagnosis Description
-(4008,0202)                         // Interpretation ID Issuer
-(4008,0102)                         // Interpretation Recorder
-(4008,010B)                         // Interpretation Text
-(4008,010A)                         // Interpretation Transcriber
(0008,3010) ~ "\\d[\\d\\.]+" : (0008,3010) := hashUID[(0008,3010)]
-(0008,3010)                         // Irradiation Event UID
-(0038,0011)                         // Issuer of Admission ID
-(0010,0021)                         // Issuer of Patient ID
-(0038,0061)                         // Issuer of Service Episode ID
(0028,1214) ~ "\\d[\\d\\.]+" : (0028,1213) := hashUID[(0028,1214)]
-(0028,1214)                         // Large Palette Color Lookup Table UID
-(0010,21D0)                         // Last Menstrual Date
-(0040,0404)                         // MAC
(0002,0003) ~ "\\d[\\d\\.]+" : (0002,0003) := hashUID[(0002,0003)]
-(0002,0003)                         // Media Storage SOP Instance UID
-(0010,2000)                         // Medical Alerts
-(0010,1090)                         // Medical Record Locator
-(0010,1080)                         // Military Rank
\end{verbatim}
\end{frame}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
-(0400,0550)                         // Modified Attributes Sequence
-(0020,3406)                         // Modified Image Description
-(0020,3401)                         // Modifying Device ID
-(0020,3404)                         // Modifying Device Manufacturer
-(0008,1060)                         // Name of Physician(s) Reading Study
-(0040,1010)                         // Names of Intended Recipient of Results
-(0010,2180)                         // Occupation
-(0008,1072)                         // Operators' Identification Sequence
-(0008,1070)                         // Operators' Name
-(0400,0561)                         // Original Attributes Sequence
-(0040,2010)                         // Order Callback Phone Number
-(0040,2008)                         // Order Entered By
-(0040,2009)                         // Order Enterer Location
-(0010,1000)                         // Other Patient IDs
-(0010,1002)                         // Other Patient IDs Sequence
-(0010,1001)                         // Other Patient Names
-(60XX,4000)                         // Overlay Comments
-(60XX,3000)                         // Overlay Data
-(0008,0024)                         // Overlay Date
-(0008,0034)                         // Overlay Time
(0028,1199) ~ "\\d[\\d\\.]+" : (0028,1199) := hashUID[(0028,1199)]
-(0028,1199)                         // Palette Color Lookup Table UID
-(0040,A07A)                         // Participant Sequence
-(0010,1040)                         // Patient Address
-(0010,4000)                         // Patient Comments
(0010,0020) := session               // Patient ID
-(0010,2203)                         // Patient Sex Neutered
-(0038,0500)                         // Patient State
-(0040,1004)                         // Patient Transport Arrangements
-(0010,1010)                         // Patient's Age
\end{verbatim}
\end{frame}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
(0010,0030) ~ ".+" : (0010,0030) := ""           // Patient's Birth Date
-(0010,1005)                         // Patient's Birth Name
-(0010,0032)                         // Patient's Birth Time
-(0038,0400)                         // Patient's Institution Residence
-(0010,0050)                         // Patient's Insurance Plan Code Sequence
-(0010,1060)                         // Patient's Mother's Birth Name
(0010,0010) := subject               // Patient's Name
-(0010,0101)                         // Patient's Primary Language Code Sequence
-(0010,0102)                         // Patient's Primary Language Modifier Code Sequence
-(0010,21F0)                         // Patient's Religious Preference
(0010,0040) ~ ".+" : (0010,0040) := ""   // Patient's Sex
-(0010,1020)                         // Patient's Size
-(0010,2154)                         // Patient's Telephone Number
-(0010,1030)                         // Patient's Weight
-(0040,0243)                         // Performed Location
-(0040,0254)                         // Performed Procedure Step Description
-(0040,0253)                         // Performed Procedure Step ID
-(0040,0244)                         // Performed Procedure Step Start Date
-(0040,0245)                         // Performed Procedure Step Start Time
-(0040,0241)                         // Performed Station AE Title
-(0040,4030)                         // Performed Station Geographic Location Code Sequence
-(0040,0242)                         // Performed Station Name
-(0040,0248)                         // Performed Station Name Code Sequence
-(0008,1052)                         // Performing Physicians' Identification Sequence
-(0008,1050)                         // Performing Physicians' Name
-(0040,1102)                         // Person Address
-(0040,1101)                         // Person Identification Code Sequence (NOTE: should be dummy value)
-(0040,A123)                         // Person Name (NOTE: should be dummy value)
\end{verbatim}
\end{frame}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
-(0040,1103)                         // Person Telephone Numbers
-(4008,0114)                         // Physician Approving Interpretation
-(0008,1062)                         // Physican Reading Study Identification Sequence
-(0008,1048)                         // Physician(s) of Record
-(0008,1049)                         // Physician(s) of Record Identification Sequence
(0040,2016) ~ ".+" : (0040,2016) := ""  // Placer Order Number of Imaging Service Request
-(0018,1004)                         // Plate ID
-(0040,0012)                         // Pre-Medication
-(0010,21C0)                         // Pregnancy Status
-(XXX#,XXXX)                         // Private Attributes
-(0018,1030)                         // Protocol Name
-(0040,2001)                         // Reason for Imaging Service Request
-(0032,1030)                         // Reason for Study
-(0400,0402)                         // Referenced Digital Signature Sequence
(3006,0024) ~ "\\d[\\d\\.]+" : (3006,0024) := hashUID[(3006,0024)]
-(3006,0024)                         // Referenced Frame of Reference UID
(0040,4023) ~ "\\d[\\d\\.]+" : (0040,4023) := hashUID[(0040,4023)]
-(0040,4023)                         // Referenced General Purpose Scheduled Procedure Step Transaction UID
-(0008,1140)                         // Referenced Image Sequence
-(0038,1234)                         // Referenced Patient Atlas Sequence
-(0008,1120)                         // Referenced Patient Sequence
-(0008,1111)                         // Referenced Performed Procedure Step Sequence
-(0400,0403)                         // Referenced SOP Instance MAC Sequence
(0008,1155) ~ "\\d[\\d\\.]+" : (0008,1155) := hashUID[(0008,1155)]                                     
\end{verbatim}
\end{frame}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
-(0008,1155)                         // Referenced SOP Instance UID
(0004,1511) ~ "\\d[\\d\\.]+" : (0004,1511) := hashUID[(0004,1511)]                                     
-(0004,1511)                         // Referenced SOP Instance UID in File
-(0008,1110)                         // Referenced Study Sequence
-(0008,0092)                         // Referring Physician's Address
-(0008,0096)                         // Referring Physician's Identification Sequence
(0008,0090) ~ ".+" : (0008,0090) := ""  // Referring Physician's Name
-(0008,0094)                         // Referring Physician's Telephone Numbers
-(0010,2152)                         // Region of Residence
(3006,00C2) ~ "\\d[\\d\\.]+" : (3006,00C2) := hashUID[(3006,00C2)]
-(3006,00C2)                         // Related Frame of Reference UID
-(0040,0275)                         // Request Attributes Sequence
-(0032,1070)                         // Requested Contrast Agent
-(0040,1400)                         // Requested Procedure Comments
-(0032,1060)                         // Requested Procedure Description
-(0040,1001)                         // Requested Procedure ID
-(0040,1005)                         // Requested Procedure Location
(0000,1001) ~ "\\d[\\d\\.]+" : (0000,1001) := hashUID[(0000,1001)]
-(0000,1001)                         // Requested SOP Instance UID
-(0032,1032)                         // Requesting Physican
-(0032,1033)                         // Requesting Service
-(0010,2299)                         // Responsible Organization
-(0010,2297)                         // Responsible Person
-(4008,4000)                         // Results Comments
-(4008,0118)                         // Results Distribution List Sequence
-(4008,0042)                         // Results ID Issuer
-(300E,0008)                         // Reviewer Name
-(0040,4034)                         // Scheduled Human Performers Sequence
-(0038,001E)                         // Scheduled Patient Institution Reference
\end{verbatim}
\end{frame}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
-(0040,000B)                         // Scheduled Performing Physician Identification Sequence
-(0040,0006)                         // Scheduled Performing Physician Name
-(0040,0004)                         // Scheduled Procedure Step End Date
-(0040,0005)                         // Scheduled Procedure Step End Time
-(0040,0007)                         // Scheduled Procedure Step Description
-(0040,0011)                         // Scheduled Procedure Step Location
-(0040,0002)                         // Scheduled Procedure Step Start Date
-(0040,0003)                         // Scheduled Procedure Step Start Time
-(0040,0001)                         // Scheduled Station AE Title
-(0040,4027)                         // Scheduled Station Geographic Location Code Sequence
-(0040,0010)                         // Scheduled Station Name
-(0040,4025)                         // Scheduled Station Name Code Sequence
-(0032,1020)                         // Scheduled Study Location
-(0032,1021)                         // Scheduled Study Location AE Title
-(0008,0021)                         // Series Date
-(0008,103E)                         // Series Description
(0020,000E) ~ "\\d[\\d\\.]+" : (0020,000E) := hashUID[(0020,000E)]
-(0020,000E)                         // Series Instance UID
-(0008,0031)                         // Series Time
-(0038,0062)                         // Service Episode Description
-(0038,0060)                         // Service Episode ID
-(0010,21A0)                         // Smoking Status
(0008,0018) := hashUID[(0008,0018)]  // SOP Instance UID
-(0008,2112)                         // Source Image Sequence
-(0038,0050)                         // Special Needs
-(0008,1010)                         // Station Name
(0088,0140) ~ "\\d[\\d\\.]+" : (0088,0140) := hashUID[(0088,0140)]
\end{verbatim}
\end{frame}
\begin{frame}[fragile]
\tiny
\begin{verbatim}
-(0088,0140)                         // Storage Media Fileset UID
-(0032,4000)                         // Study Comments
(0008,0020) ~ ".+" : (0008,0020) := ""  // Study Date
(0008,1030) := project               // Study Description
(0020,0010) ~ ".+" : (0020,0010) := ""  // Study ID
-(0032,0012)                         // Study ID Issuer
(0020,000D) := hashUID[(0020,000D)]  // Study Instance UID
(0008,0030) ~ ".+" : (0008,0030) := ""  // Study Time
(0020,0200) ~ "\\d[\\d\\.]+" : (0020,0200) := hashUID[(0020,0200)]
-(0020,0200)                         // Synchronization Frame of Reference UID
(0040,DB0D) ~ "\\d[\\d\\.]+" : (0040,DB0D) := hashUID[(0040,DB0D)]
-(0040,DB0D)                         // Template Extension Creator UID
(0040,DB0C) ~ "\\d[\\d\\.]+" : (0040,DB0C) := hashUID[(0040,DB0C)]
-(0040,DB0C)                         // Template Extension Organization UID
-(4000,4000)                         // Text Comments
-(2030,0020)                         // Text String
-(0008,0201)                         // Timezone Offset From UTC
-(0088,0910)                         // Topic Author
-(0088,0912)                         // Topic Key Words
-(0088,0906)                         // Topic Title
(0008,1195) ~ "\\d[\\d\\.]+" : (0008,1195) := hashUID[(0008,1195)]
-(0008,1195)                         // Transaction UID
(0040,A124) ~ "\\d[\\d\\.]+" : (0040,A124) := hashUID[(0040,A124)]
-(0040,A124)                         // UID
-(0040,A088)                         // Verifying Observer Identification Code Sequence
-(0040,A075)                         // Verifying Observer Name (NOTE: should be dummy value)
-(0040,A073)                         // Verifying Observer Sequence (NOTE: should be dummy value)
-(0040,A027)                         // Verifying Organization
-(0038,4000)                         // Visit Comments
\end{verbatim}
\end{frame}

\section{In XNAT}
\subsection{Upload applet}
\begin{frame}{DicomEdit in XNAT upload applet}
  The XNAT upload applet reads the site-wide script, plus the
  project's DicomEdit script, if available, from the config
  service. These can be set in the webapp from the admin configuration
  and project management pages.

DicomEdit for XNAT includes some special features (makeSessionLabel and export).
\end{frame}

\subsection{XNAT}
\begin{frame}{DicomEdit in XNAT archiving}
XNAT uses DicomEdit to modify DICOM metadata, applying:
\begin{itemize}
\pause \item the site-wide script as objects are received
\pause \item the project script to each session as it is archived
\pause \item the destination project script to each session moved
             from one project to another
\end{itemize}
\end{frame}

\section{The future}
\begin{frame}{Future features}
\begin{itemize}
\pause \item Better error messages and debugging support
\pause \item Syntax-aware editor
\pause \item Value VR-compatibility checking
\pause \item Integration/consolidation with MIRC Anonymizer language
\end{itemize}
\end{frame}

\end{document}
